"""
# Reductive algebraic groups and root data

Let  us fix an  algebraically closed field  `K` and let  `ð†` be a connected
reductive  algebraic group over `K`. Let `ð“` be a maximal torus of `ð†`, let
`X(ð“)`  be the  character group  of `ð“`  (resp. `Y(ð“)`  the dual lattice of
one-parameter  subgroups  of  `ð“`)  and  `Î¦`  (resp  `Î¦^`) the roots (resp.
coroots) of `ð†` with respect to `ð“`.

Then  `ð†` is  determined up  to isomorphism  by the  *root datum* `(X(ð“),Î¦,
Y(ð“),Î¦^)`.  In algebraic terms, this consists  in giving a free `â„¤`-lattice
`X=X(ð“)` of dimension the *rank* of `ð“` (which is also called the *rank* of
`ð†`),  and a root system `Î¦ âŠ‚ X`,  and similarly giving the dual roots `Î¦^âŠ‚
Y=Y(ð“)`.

This  is obtained  by a  slight generalisation  of our  setup for a Coxeter
group `W`. This time we assume that the canonical basis of the vector space
`V`  on which `W`  acts is a  `â„¤`-basis of `X`,  and `Î¦` is specified by an
integral  matrix `simpleroots(W)` whose rows are the simple roots expressed
in  this basis of  `X`. Similarly `Î¦^`  is described by  an integral matrix
`simplecoroots(W)`  whose rows are  the simple coroots  in the basis of `Y`
dual to the chosen basis of `X`. The duality pairing between `X` and `Y` is
the canonical one, that is the pairing between vectors `xâˆˆ X` and `yâˆˆ Y` is
given by `transpose(x)*y`. Thus, we must have the relation
`simplecoroots(W)*permutedims(simpleroots(W))=cartan(W)`.

We  get this by using the function `rootdatum`, whose arguments are the two
matrices `simpleroots(W)` and `simplecoroots(W)` described above. The roots
need not generate `V`, so the matrices need not be square. For example, the
root datum of the linear group of rank 3 can be obtained as:

```julia-repl
julia> W=rootdatum([-1 1 0;0 -1 1],[-1 1 0;0 -1 1])
Aâ‚‚Î¦â‚

julia> reflrep(W,W(1))
3Ã—3 Matrix{Int64}:
 0  1  0
 1  0  0
 0  0  1
```
here  the  Weyl  group  is  the  symmetric  group  on  3  letters acting by
permutation of the basis of `X`. The dimension of `X`,
`size(simpleroots(W),2)`,  is the *rank* and  the dimension of the subspace
generated   by   the   roots,   `size(simpleroots(W),1)`,   is  called  the
*semi-simple rank*. In the example the rank is 3 and the semisimple rank is 2.

The  default representation  obtained as  `W=coxgroup(:A,2)` corresponds to
the  adjoint algebraic group (the group in its isogeny class with a trivial
centre).  In this case  `Î¦` is a  basis of `X`,  so `simpleroots(W)` is the
identity  matrix and `simplecoroots(W)` is the Cartan matrix `cartan(W)` of
the root system.

The   form  `coxgroup(:A,2,sc=true)`   constructs  the   semisimple  simply
connected  algebraic  group,  where  `simpleroots(W)`  is the transposed of
`cartan(W)` and `simplecoroots(W)` is the identity matrix.

An  extreme form of root data can  be specified more conveniently: when `W`
is  the trivial `coxgroup()` (in this case `ð† ` is a torus), the root datum
has  no roots, thus  is entirely determined  by the rank  `r`. The function
`torus(r)`  constructs  such  a  root  datum  (it could be also obtained by
giving two `0Ã—r` matrices to `rootdatum`).

Finally,  the `rootdatum` function also understands some familiar names for
the  algebraic groups for which it gives the results that could be obtained
by giving the appropriate `simpleroots(W)` and `simplecoroots(W)` matrices:
```julia-repl
julia> rootdatum(:gl,3)   # same as the previous example
glâ‚ƒ
```
The types of root data which are understood are
 `:gl, :pgl, :sl, :slmod, :tgl :sp, :csp, :psp, :so, :pso, :cso, :halfspin, 
  :gpin, :spin, :E6, :E6sc, :CE6, :E7, :E7sc, :CE7, :E8, :F4, :G2`.
"""
module Rootdata
using ..Chevie
export torus, istorus, derived_datum, SubTorus, weightinfo, fundamental_group,
       intermediate_group, isomorphism_type, weights, coweights
"""
`torus(rank::Integer)`

This  function returns the object corresponding to the notion of a torus of
dimension  `rank`, a Coxeter  group of semisimple  rank 0 and given `rank`.
This  corresponds to a split torus; the extension to Coxeter cosets is more
useful.

```julia-repl
julia> torus(3)
Î¦â‚Â³
```
"""
torus(i::Integer)=Weyl.FCG(PRG(i),Vector{Int}[],0,Dict{Symbol,Any}())

"`istorus(W)` whether `W` is a torus"
istorus(W)=isempty(roots(W))

PermRoot.radical(W::Weyl.FC)=torus(rank(W)-semisimplerank(W))

function uniinv(m) # inverse of unimodular matrix
  ch,c=GenLinearAlgebra.charpolyandcomatrix(m)
  (-1)^size(m,1)*ch[1]*c
end

"""
`derived_datum(G)`

The  derived datum of `(X,Î¦,Y,Î¦^âˆ¨)` is `(X/Î¦^{âˆ¨âŸ‚}, Î¦, Yâˆ© â„š Î¦^âˆ¨, Î¦^âˆ¨)` where
`âŸ‚`  denotes the orthogonal. This function  starts with a root datum object
`G` and returns the root datum object corresponding to the derived datum.
"""
function derived_datum(G::Weyl.FC)
  s=semisimplerank(G)
# Find a basis of Y where the last rank(G)-s columns of simplecoroots are 0
# Then  in  this  basis  Yâˆ©  â„š  Î¦^âˆ¨  is  the â„¤-space spanned by the first s
# columns.  (gv^-t,gv) is a  (co)-isomorphism of root  data G->G1 and in G1
# projecting on the first s columns gives the derived datum.
  gv=col_hermite_transforms(simplecoroots(G)).coltrans
  g=uniinv(permutedims(gv))  # so gv = g^-t.
  rootdatum(simpleroots(G)*g[:,1:s],simplecoroots(G)*gv[:,1:s])
end

struct SubTorus{T<:Integer}
  gens::Matrix{T}
  complement::Matrix{T}
  group
end

"""
`SubTorus(W,Y::Matrix{<:Integer})`

The  function  returns  the  subtorus  ð’  of  the  maximal torus `ð“` of the
reductive  group represented by the Weyl group  `W` such that `Y(ð’)` is the
(pure)  sublattice of  `Y(ð“)` generated  by the  (integral) vectors  `Y`. A
basis  of `Y(ð’)` in  Hermite normal form  (for easy memebership testing) is
computed  and stored in the field `S.gens` of the returned SubTorus struct.
A  basis of `Y(ð“)` adapted to `Y(ð’)` is  also computed. This means a set of
integral   vectors,  stored  in  `S.complement`,   is  computed  such  that
`M=vcat(S.gens,S.complement)`   is   a   basis   of   `Y(ð“)`  (equivalently
`MâˆˆGL(Z^{rank(W)})`.  An  error  is  raised  if  `Y` does not define a pure
sublattice.

```julia-repl
julia> W=coxgroup(:A,4)
Aâ‚„

julia> SubTorus(W,[1 2 3 4;2 3 4 1;3 4 1 1])
SubTorus(Aâ‚„,[1 0 3 -13; 0 1 2 7; 0 0 4 -3])

julia> SubTorus(W,[1 2 3 4;2 3 4 1;3 4 1 2])
ERROR: not a pure sublattice
Stacktrace:
 [1] error(::String) at ./error.jl:33
 [2] Chevie.Weyl.SubTorus(::FiniteCoxeterGroup{Perm{Int16},Int64}, ::Matrix{Int64}) at /home/jmichel/julia/Chevie.jl/src/Weyl.jl:1082
 [3] top-level scope at REPL[25]:1
```
"""
function SubTorus(W,V::Matrix{<:Integer}=reflrep(W,one(W)))
  C=complementInt(V)
  if any(!=(1),C.moduli) error("not a pure sublattice") end
  SubTorus(baseInt(C.sub),C.complement,W)
end

Base.show(io::IO,T::SubTorus)=print(io,"SubTorus(",T.group,",",T.gens,")")

Chevie.rank(T::SubTorus)=size(T.gens,1)

#----------------------- Fundamental group --------------------------
function WeightToAdjointFundamentalGroupElement(W,l::Vector;full=false)
  if isempty(l) return Perm();end
  prod(x->WeightToAdjointFundamentalGroupElement(W,x;full),l)
end

function WeightToAdjointFundamentalGroupElement(W,i;full=false)
  t=refltype(W)[findfirst(t->i in t.indices,refltype(W))]
  l=copy(t.indices)
  b=longest(W,l)*longest(W,setdiff(l,[i]))
  push!(l,maximum(findall(
    i->all(j->j in t.indices || W.rootdec[i][j]==0,1:semisimplerank(W)),
  eachindex(W.rootdec))))
  full ? b : restricted(b,inclusion.(Ref(W),l))
end

"""
`weightinfo(W)`

`W`  is a  Coxeter group  record describing  an algebraic  group `ð† `, or a
`IypeIrred`. The function is independent of the isogeny type of `ð†`(so just
depends  on `refltype(W)`, that is  on the root system).  It returns a dict
with the following keys:

  - `:minusculeWeights` the minuscule weights, described as their position 
    in  the  list  of  fundamental  weights.  For non-irreducible groups, a
    weight  is the sum of at most one weight in each irreducible component.
    It  is represented as  the list of  its components. For consistency, in
    the  case  of  an  irreducible  system,  a  weight  is represented as a
    one-element list.

  - `:minusculeCoweights` the minuscule coweights, represented in the same
    manner as the minuscule weights

  - `:decompositions` for each coweight, its decomposition in terms of the
    generators  of the adjoint fundamental group  (given by the list of the
    exponents  of the generators). Together with  the next field it enables
    to work out the group structure of the adjoint fundamental group.

  - `:moduli` the list of orders of the generators of the fundamental group.

  - `:AdjointFundamentalGroup` the list of generators of the adjoint
    fundamental group, given as permutations of the extended root basis.

  - `:CenterSimplyConnected` A list of semisimple elements generating the
    center of the universal covering of  ð†

  - `:chosenAdaptedBasis` A basis  of the  weight lattice  adapted to the
    root lattice. In the basis of the fundamental weights, the root lattice
    is  given  by  `C=transpose(cartan(W))`.  The  property  specifying the
    `:chosenAdaptedBasis` is that the Hermite normal form of
    `C*chosenAdaptedBasis`  is almost in Smith  normal form (it is diagonal
    but  the diagonal entries may be  permuted compared to the Smith normal
    form; the non-trivial entries are in the positions corresponding to the
    generators of the fundamental group as indicated by `:decompositions`).

```julia-repl
julia> weightinfo(coxgroup(:A,2)*coxgroup(:B,2))
Dict{Symbol, Array} with 8 entries:
  :moduli                  => [3, 2]
  :minusculeWeights        => [[1, 3], [1], [2, 3], [2], [3]]
  :decompositions          => [[2, 1], [2, 0], [1, 1], [1, 0], [0, 1]]
  :highestroot             => [5, 7]
  :chosenAdaptedBasis      => [1 -1 0 0; 0 1 0 0; 0 0 1 0; 0 0 0 1]
  :minusculeCoweights      => [[1, 4], [1], [2, 4], [2], [4]]
  :CenterSimplyConnected   => Vector{Rational{Int64}}[[1//3, 2//3, 0, 0], [0, 0â€¦
  :AdjointFundamentalGroup => [(1,12,2), (4,14)]
```
"""
function weightinfo(t::TypeIrred)
  r=chevieget(t,:WeightInfo)
  if isnothing(r)
    r=Dict{Symbol,Any}(:moduli=>Int[],:decompositions=>Vector{Vector{Int}}[],
         :minusculeWeights=>Vector{Int}[])
  end
  if !haskey(r,:minusculeCoweights)
    r[:minusculeCoweights]=r[:minusculeWeights]
  end
  if !haskey(r,:chosenAdaptedBasis)
    r[:chosenAdaptedBasis]=Matrix{Int}(I,rank(t),rank(t))
  end
  r
end

function weightinfo(W)
  get!(W,:weightinfo)do
  M=Matrix{Int}(I,semisimplerank(W),semisimplerank(W))
  if isempty(refltype(W)) return Dict(:minusculeWeights=>Vector{Int}[],
         :minusculeCoweights=>Vector{Int}[],
         :decompositions=>Vector{Vector{Int}}[],
         :moduli=>Int[],
         :highestroot=>Int[],
         :CenterSimplyConnected=>Vector{Rational{Int}}[],
         :AdjointFundamentalGroup=>eltype(W)[]
        )
  end
  l=map(refltype(W)) do t
    r=weightinfo(t)
    if isempty(r[:moduli]) g=Int[]
      r[:ww]=eltype(W)[]
    else g=filter(i->sum(r[:decompositions][i])==1,
          eachindex(r[:minusculeCoweights])) # generators of fundamental group
      r[:ww]=map(x->WeightToAdjointFundamentalGroupElement(W,x),
               t.indices[r[:minusculeCoweights][g]])
    end
    r[:csi]=zeros(Rational{Int},length(g),semisimplerank(W))
    if !isempty(r[:moduli])
      C=modZ.(inv(Rational.(cartan(t))))
      r[:csi][:,t.indices]=C[r[:minusculeCoweights][g],:]
      r[:minusculeWeights]=t.indices[r[:minusculeWeights]]
      r[:minusculeCoweights]=t.indices[r[:minusculeCoweights]]
      M[t.indices,t.indices]=r[:chosenAdaptedBasis]
    end
    r[:csi]=Array.(toL(r[:csi]))
    r[:highestroot]=last(findmax(x->sum(x[indices(t)]),W.rootdec))
    r
  end
  res=Dict(:minusculeWeights=>cartesian(map(
                                   x->vcat(x[:minusculeWeights],[0]),l)...),
    :minusculeCoweights=>cartesian(map(
                                   x->vcat(x[:minusculeCoweights],[0]),l)...),
    :decompositions=>splat(vcat).(tcartesian(map(x->vcat(x[:decompositions],
                                 [0 .*x[:moduli]]),l)...)),
    :moduli=>reduce(vcat,map(x->x[:moduli],l)),
# center of simply connected group: the generating minuscule coweights
# mod the root lattice
    :CenterSimplyConnected=>reduce(vcat,getindex.(l,:csi)),
    :AdjointFundamentalGroup=>reduce(vcat,getindex.(l,:ww)),
    :chosenAdaptedBasis=>M,
    :highestroot=>map(x->x[:highestroot],l))
  n=1:length(res[:decompositions])-1
  res[:minusculeWeights]=map(x->filter(!iszero,x),res[:minusculeWeights][n])
  res[:minusculeCoweights]=map(x->filter(!iszero,x),res[:minusculeCoweights][n])
  res[:decompositions]=res[:decompositions][n]
  res
  end
end

" `weights(W)` simple weights in the basis of X(T)"
weights(W)=permutedims(inv(Rational.(cartan(W))))*simpleroots(W)

" `coweights(W)` simple coweights in the basis of Y(T)"
coweights(W)=inv(Rational.(cartan(W)))*simplecoroots(W)

"""
`fundamental_group(W)`

This  function returns the fundamental group of the algebraic group defined
by  the Coxeter  group struct  `W`. This  group is  returned as  a group of
diagram  automorphisms  of  the  corresponding  affine Weyl group, which is
represented  as a group of permutations of the set of simple roots enriched
by the lowest root of each irreducible component. The definition we take of
the  fundamental group of a (not necessarily semisimple) reductive group is
(Pâˆ© Y(ð“))/Q where P is the coweight lattice (the dual lattice in Y(ð“)âŠ— â„š of
the  root  lattice)  and  Q  is  the  coroot  latice. The bijection between
elements  of P/Q and  diagram automorphisms is  explained in the context of
non-irreducible groups for example in [bon05; Â§3.B](@cite).

```julia-repl
julia> W=coxgroup(:A,3) # the adjoint group
Aâ‚ƒ

julia> fundamental_group(W) # the 12th root is the lowest one
Group((1,2,3,12))

julia> W=rootdatum(:sl,4) # the semisimple simply connected group
slâ‚„

julia> fundamental_group(W)
Group(Perm{Int16}[])
```
"""
function fundamental_group(W;full=false)
  if istorus(W) return Group(Perm()) end
  e=weightinfo(W)[:minusculeCoweights]
  e=filter(x->all(isinteger,sum(coweights(W)[x,:];dims=1)),e) # minusc. coweights in Y
  if isempty(e) return Group(Perm()) end
  e=map(x->WeightToAdjointFundamentalGroupElement(W,x;full),e)
  Group(abelian_gens(e))
end

"""
`intermediate_group(W, indices)`

returns a `rootdatum` which represents a semisimple algebraic group between
the   adjoint  group,  obtained  by  a  call  like  `rootdatum(:pgl,4)`  or
`coxgroup(:A,3)`, and the simply connected semi-simple group, obtained by a
call  like  `rootdatum(:sl,4)`  or  `coxgroup(:A,3;sc=true)`. This group is
specified  by a subset of the *minuscule weights*, the weights whose scalar
product  with each  coroot is  in `-1,0,1`  (a weight  is an element of the
*weight  lattice*  `P`,  the  lattice  dual  to  the  coroot  lattice). The
non-trivial  characters of the  (algebraic) center of  a semi-simple simply
connected  algebraic group are in bijection with the minuscule weights, and
also  in bijection  with `P/Q`  where `Q`  is the  root lattice.  If `W` is
irreducible,  the minuscule  weights are  part of  the basis  of the weight
lattice  given by the *fundamental weights*, which is the dual basis of the
simple  coroots. They can therefore be specified  by an index in the Dynkin
diagram.  The  `indices`  of  minuscule  weights  in the dynkin diagram are
indices  where the coefficient of  the highest root on  the simple roots is
`1`. The constructed intermediate group has lattice `X(ð“)` generated by the
root  lattice and the given minuscule weights. If `W` is not irreducible, a
minuscule  weight is a sum of minuscule weights in different components; an
element of `indices` is thus itself a list, interpreted as representing the
sum of the corresponding weights.

```julia-repl
julia> W=coxgroup(:A,3)
Aâ‚ƒ

julia> fundamental_group(intermediate_group(W,Int[])) # adjoint
Group((1,2,3,12))

julia> fundamental_group(intermediate_group(W,Int[2])) # intermediate
Group((1,3)(2,12))
```
"""
function intermediate_group(W,I)
  C=cartan(W)
  w=transpose(inv(Rational.(C))); # w = weights in terms of roots
  R=one(w)
  for v in I
    if v isa Integer R=vcat(R,w[[v],:])
    else R=vcat(R,sum(w[v,:],dims=1))
    end
  end
  d=lcm(denominator.(R))
  R=baseInt(Integer.(d*R))//d
  rootdatum(Integer.(R^-1),Integer.(C*transpose(R)))
end

function isomorphism_type(t::TypeIrred;TeX=false,limit=false)
  if !limit && !TeX context=(:TeX=>true,:limit=>false)
  else context=(:TeX=>TeX,:limit=>limit)
  end
  t=repr(t;context)
  if !limit && !TeX
    t=Format.TeXstrip(t)
    t=replace(t,"^"=>"")
  end
  t
end

function isomorphism_type(W;torus=false,TeX=false,limit=false)
  if !limit && !TeX context=(:TeX=>true,:limit=>false)
  else context=(:TeX=>TeX,:limit=>limit)
  end
  t=reverse(tally(map(x->isomorphism_type(x;TeX,limit),refltype(W))))
  t=join(map(x-> x[2]==1 ? x[1] : string(x[2],x[1]),t),"+")
  d=rank(W)-semisimplerank(W)
  if d>0 && torus
    if t!="" t*="+" end
    t*="T"*string(d)
  end
  t
end

end
