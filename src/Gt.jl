module Gt
using ..Chevie
export RationalUnipotentClasses, closed_subsystems, ClassTypes

function RationalUnipotentClasses(WF, p=0)
  u=UnipotentClasses(WF, p)
  t=Ucl.XTable(u;classes=true)
  map(i->(card=CycPol(t.cardClass[i]), class=u.classes[t.classes[i][1]], 
          classno=t.classes[i][1], AuNo=t.classes[i][2]), eachindex(t.classes))
end

# build the 2NÃ—2N array sumr: sumr[i,j]=k if root(W,i)+root(W,j)=root(W,k)
#                                       0 if root(W,i)+root(W,j) is not a root
function sumr(W)
  get!(W,:sumr)do
    function possum(i,j)
      p=findfirst(==(roots(W,i)+roots(W,j)),roots(W))
      isnothing(p) ? 0 : p
    end
    [possum(i,j) for i in 1:2nref(W),  j in 1:2nref(W)]
  end::Matrix{Int}
end

# closure of the subset roots(W,l)
function closure(W,l)
  psum=sumr(W)
  lset=Set(l)
  res=Int[]
  l=copy(l)
  for r in l
    for s in res
      u=psum[r,s]
      if !iszero(u) && !(u in lset) 
        push!(l,u) 
        push!(lset,u)
      end
    end
    push!(res,r)
  end
  sort!(l)
end

"""
`closed_subsystems(W)` 

`W`  should  be  a  Weyl  group.  The  function returns the Poset of closed
subsystems  of the root system of `W`. Each closed subsystem is represented
by  the list of indices of its simple roots.  If `W` is the Weyl group of a
reductive  group  `ð†  `,  then  closed  subsystem  correspond  to reductive
subgroups of maximal rank. And all such groups are obtained this way, apart
from  some  exceptions  in  characteristics  2  and 3 (see [Malle-Testerman
2011](biblio.htm#MT11) Proposition 13.4).

```julia-repl
julia> W=coxgroup(:G,2)
Gâ‚‚

julia> closed_subsystems(W)
1 2<1 4<4<âˆ…
1 2<1 5<1<âˆ…
1 2<2 6<6<âˆ…
1 2<3 5<5<âˆ…
1 4<1
1 5<6
1 5<5
2 6<2<âˆ…
3 5<3<âˆ…
```
"""
function closed_subsystems(W)
  get!(W, :closedsubsets)do
  l = [Int[]]
  ldict=Dict{Vector{Int},Int}()
  new = [1]
  covers = Tuple{Int,Int}[]
  for w in new
    for f in setdiff(1:nref(W), l[w])
      n=closure(W,vcat(l[w],[f,f+nref(W)]))
      if haskey(ldict,n)
        push!(covers,(ldict[n], w))
      else
        push!(l, n)
        ldict[n]=length(l)
        push!(covers,(length(l),w))
        push!(new, length(l))
      end
    end
  end
  P=Poset(CPoset(covers),l)
  P.show_element=function(io,x,n)
    e=Weyl.simpleroots_subsystem(W,x.elements[n])
    print(io,isempty(e) ? "âˆ…" : join(e," "))
  end
  P
  end
end

function closed_subsystems(W::CoxeterCoset)
  get!(W, :closedsubsets)do
    P=closed_subsystems(Group(W))
    P=induced(P,filter(x->onsets(x,W.phi)==x,P.elements))
    P.show_element=function(io,x,n)
      e=Weyl.simpleroots_subsystem(Group(W),x.elements[n])
      print(io,isempty(e) ? "âˆ…" : join(e," "))
    end
    P
  end
end

@GapObj struct ClassType
  CGs
  cent::CycPol
  unip
end

@GapObj struct ClassTypes
  p::Int
  WF
  ss::Vector{ClassType}
end

"""
`ClassTypes(G[,p])`

`G`  should be a root  datum or a twisted  root datum representing a finite
reductive  group ``ð† ^F`` and  `p` should be a  prime. The function returns
the class types of `G` in characteristic `p` (in good characteristic if `p`
is  omitted). Two elements  of ``ð† ^F``  have the same  class type if their
centralizers  are  conjugate.  If  `su`  is  the Jordan decomposition of an
element  `x`, the class type of `x` is  determined by the class type of its
semisimple part `s` and the unipotent class of `u` in ``C_ð† (s)``.

The   function  `ClassTypes`  is  presently  only  implemented  for  simply
connected  groups, where  ``C_ð† (s)``  is connected.  This section is a bit
experimental and may change in the future.

`ClassTypes`  returns a  `struct` which  contains a  list of classtypes for
semisimple  elements,  which  are  represented  by  `subspets`  and contain
additionnaly information on the unipotent classes of ``C_ð† (s)``.

Let us give some examples:

```julia-repl
julia> t=ClassTypes(rootdatum(:sl,3))
ClassTypes(Aâ‚‚,good characteristic)
â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
â”‚C_G(s)    â”‚ |C_G(s)|â”‚
â”œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¼â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¤
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Â²  â”‚      Î¦â‚Â²â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚     Î¦â‚Î¦â‚‚â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚ƒ   â”‚       Î¦â‚ƒâ”‚
â”‚Aâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚â”‚   qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Aâ‚‚        â”‚qÂ³Î¦â‚Â²Î¦â‚‚Î¦â‚ƒâ”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```
By   default,  only  information  about  semisimple  centralizer  types  is
returned:   the type, and its generic order.

```julia-rep1
julia> xdisplay(t;unip=true)
ClassTypes(Aâ‚‚,good characteristic)
â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
â”‚C_G(s)    â”‚    u |C_G(su)|â”‚
â”œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¼â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¤
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Â²  â”‚    1       Î¦â‚Â²â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚    1      Î¦â‚Î¦â‚‚â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚ƒ   â”‚    1        Î¦â‚ƒâ”‚
â”‚Aâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚â”‚   11    qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚          â”‚    2       qÎ¦â‚â”‚
â”‚Aâ‚‚        â”‚  111 qÂ³Î¦â‚Â²Î¦â‚‚Î¦â‚ƒâ”‚
â”‚          â”‚   21      qÂ³Î¦â‚â”‚
â”‚          â”‚    3       3qÂ²â”‚
â”‚          â”‚ 3_Î¶â‚ƒ       3qÂ²â”‚
â”‚          â”‚3_Î¶â‚ƒÂ²       3qÂ²â”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```
Here  we  have  displayed  information  on  unipotent  classes,  with their
centralizer.

```julia-rep1
julia> xdisplay(t;nClasses=true)
ClassTypes(Aâ‚‚,good characteristic)
â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
â”‚C_G(s)    â”‚       nClasses  |C_G(s)|â”‚
â”œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¼â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¤
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Â²  â”‚(qÂ²-5q+2qâ‚ƒ+4)/6       Î¦â‚Â²â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚       (qÂ²-q)/2      Î¦â‚Î¦â‚‚â”‚
â”‚Aâ‚‚â‚â‚Ž=Î¦â‚ƒ   â”‚  (qÂ²+q-qâ‚ƒ+1)/3        Î¦â‚ƒâ”‚
â”‚Aâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚â”‚       (q-qâ‚ƒ-1)    qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Aâ‚‚        â”‚             qâ‚ƒ qÂ³Î¦â‚Â²Î¦â‚‚Î¦â‚ƒâ”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```
Here  we have added information on how many semisimple conjugacy classes of
`ð†  ^F` have a given type. The  answer in general involves variables of the
form `qâ‚` which represent `gcd(q-1,a)`.

Finally an example in bad characteristic:

```julia-rep1
julia> t=ClassTypes(coxgroup(:G,2),2);xdisplay(t;nClasses=true)
ClassTypes(Gâ‚‚,char. 2)
ClassTypes(Gâ‚‚,char. 2)
â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
â”‚C_G(s)    â”‚         nClasses     |C_G(s)|â”‚
â”œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¼â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¤
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Â²  â”‚(qÂ²-8q+2qâ‚ƒ+10)/12          Î¦â‚Â²â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚        (qÂ²-2q)/4         Î¦â‚Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚        (qÂ²-2q)/4         Î¦â‚Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚†   â”‚    (qÂ²-q-qâ‚ƒ+1)/6           Î¦â‚†â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚ƒ   â”‚    (qÂ²+q-qâ‚ƒ+1)/6           Î¦â‚ƒâ”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚‚Â²  â”‚ (qÂ²-4q+2qâ‚ƒ-2)/12          Î¦â‚‚Â²â”‚
â”‚Gâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚â”‚       (q-qâ‚ƒ-1)/2       qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚‚â”‚       (q-qâ‚ƒ+1)/2       qÎ¦â‚Î¦â‚‚Â²â”‚
â”‚Gâ‚‚â‚â‚‚â‚Ž=AÌƒâ‚Î¦â‚â”‚          (q-2)/2       qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚‚â‚Ž=AÌƒâ‚Î¦â‚‚â”‚              q/2       qÎ¦â‚Î¦â‚‚Â²â”‚
â”‚Gâ‚‚        â”‚                1 qâ¶Î¦â‚Â²Î¦â‚‚Â²Î¦â‚ƒÎ¦â‚†â”‚
â”‚Gâ‚‚â‚â‚â‚…â‚Ž=Aâ‚‚ â”‚         (qâ‚ƒ-1)/2    qÂ³Î¦â‚Â²Î¦â‚‚Î¦â‚ƒâ”‚
â”‚Gâ‚‚â‚â‚â‚…â‚Ž=Â²Aâ‚‚â”‚         (qâ‚ƒ-1)/2    qÂ³Î¦â‚Î¦â‚‚Â²Î¦â‚†â”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```
We  notice that if `q` is  a power of `2` such  that `qâ‰¡2 (mod 3)`, so that
`qâ‚ƒ=1`,  some class types do not exist. We can see what happens by giving a
specific value to `qâ‚ƒ`:

```julia-rep1
julia> xdisplay(t(;q_3=1);nClasses=true)
ClassTypes(Gâ‚‚,char. 2) qâ‚ƒ=1
â”Œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¬â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”
â”‚C_G(s)    â”‚     nClasses     |C_G(s)|â”‚
â”œâ”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¼â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”¤
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Â²  â”‚(qÂ²-8q+12)/12          Î¦â‚Â²â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚    (qÂ²-2q)/4         Î¦â‚Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚Î¦â‚‚ â”‚    (qÂ²-2q)/4         Î¦â‚Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚†   â”‚     (qÂ²-q)/6           Î¦â‚†â”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚ƒ   â”‚     (qÂ²+q)/6           Î¦â‚ƒâ”‚
â”‚Gâ‚‚â‚â‚Ž=Î¦â‚‚Â²  â”‚   (qÂ²-4q)/12          Î¦â‚‚Â²â”‚
â”‚Gâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚â”‚      (q-2)/2       qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚â‚Ž=Aâ‚Î¦â‚‚â”‚          q/2       qÎ¦â‚Î¦â‚‚Â²â”‚
â”‚Gâ‚‚â‚â‚‚â‚Ž=AÌƒâ‚Î¦â‚â”‚      (q-2)/2       qÎ¦â‚Â²Î¦â‚‚â”‚
â”‚Gâ‚‚â‚â‚‚â‚Ž=AÌƒâ‚Î¦â‚‚â”‚          q/2       qÎ¦â‚Î¦â‚‚Â²â”‚
â”‚Gâ‚‚        â”‚            1 qâ¶Î¦â‚Â²Î¦â‚‚Â²Î¦â‚ƒÎ¦â‚†â”‚
â””â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”´â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”˜
```
"""
function ClassTypes(W,p=0)
  if W isa Spets WF=W;W=Group(W)
  else WF=spets(W)
  end
  l=vcat(twistings.(Ref(WF),sscentralizer_reps(Group(WF), p))...)
  ClassTypes(p,WF,map(x->ClassType(x,CycPol(generic_order(x,Pol(:q))),
    RationalUnipotentClasses(x,p),Dict{Symbol,Any}()),l),Dict{Symbol,Any}())
end

bracket_if_needed(v)=occursin(r"[-+*/]",v[nextind(v,0,2):end]) ? "("*v*")" : v

function Base.show(io::IO,r::ClassTypes)
  res=string("ClassTypes(\$",TeX(io,r.WF),"\$")
  if r.p==0 res*=",good characteristic)"
  else res*=string(",char. ",r.p,")")
  end
  if haskey(r,:specialized)
    res*=string(" \$",join(map(x->string(x[1],"=",x[2]),collect(r.specialized))," "),"\$")
  end
  printTeX(io,res*"\n")
  function nc(p)
    p=Mvp(p)
    d=lcm(denominator.(values(p.d)))
    p=bracket_if_needed(xrepr(io,improve_type(p*d)))
    d==1 ? p : string(p,"/",d)
  end
  classes=get(io,:nClasses,false)
  col_labels=String[]
  if classes push!(col_labels, "nClasses") end
  if get(io,:unip,false)
    row_labels=[]
    push!(col_labels,"u")
    push!(col_labels,"|C_G(su)|")
    t=[]
    for x in r.ss
      u=RationalUnipotentClasses(x.CGs, r.p)
      for c in u
        v=String[]
        if isone(c.card)
          if classes push!(v, nc(nconjugacy_classes(x,r.WF,r.p))) end
          push!(row_labels,TeX(io,x.CGs))
        else
          push!(row_labels," ")
          if classes push!(v,"") end
        end
        push!(v,Ucl.nameclass(merge(c.class.prop,Dict(:name=>c.class.name)),
                              merge(io.dict,Dict(:class=>c.AuNo))))
        push!(v,xrepr(io,x.cent//c.card))
        push!(t,v)
      end
    end
    t=toM(t)
  else
    row_labels=map(x->TeX(io,x.CGs),r.ss)
    t=[]
    if classes push!(t,nc.(nconjugacy_classes(r))) end
    push!(t,map(x->xrepr(io,x.cent),r.ss))
    push!(col_labels,"|C_G(s)|")
    t=permutedims(toM(t))
  end
  showtable(io,t;col_labels,row_labels,rows_label="C_G(s)")
end

function (C::ClassTypes)(;arg...)
  C=deepcopy(C)
  C.specialized=arg
  C.ss.=map(function (x)
           res=deepcopy(x)
           res.nClasses=nconjugacy_classes(x,C.WF,C.p)(;arg...)
           res
          end, C.ss)
  filter!(r->!iszero(nconjugacy_classes(r,C.WF,C.p)),C.ss)
  C
end

function nconjugacy_classes(C::ClassTypes)
  W=Group(C.WF)
  if length(fundamental_group(W))>1
    print("# Nr classes each type implemented only for simply connected groups")
    return
  end
  filter!(r->nconjugacy_classes(r,C.WF,C.p)!=0,C.ss)
  nconjugacy_classes.(C.ss,Ref(C.WF),C.p)
end

# See Fleischmann-Janiszczak AAECC 1996 definition 2.1
function nconjugacy_classes(r::ClassType,WF,p)
  get!(r,:nClasses)do
    HF=r.CGs
    H=Group(HF)
    W=Group(WF)
    P=deepcopy(closed_subsystems(W))
    elts=P.elements
    elts=filter(x->onsets(x,HF.phi)==x && issubset(inclusiongens(H),x),elts)
    P=induced(P,elts)
# here P poset of HF.phi-stable closed subsets containing roots(H)
#   InfoChevie("# ", HF, "==>", P, "")
    l=map(x->spets(reflection_subgroup(W, x), HF.phi), P.elements)
    b=collect(keys(factor(PermRoot.BadNumber(W))))
    l=map(l)do RF
      R=Group(RF)
      res=reduce(*,map(p->Mvp(:q)-p[2],filter(y->y[1]==1,degrees(RF)));init=Mvp(1))
      if semisimplerank(R)==0 return res end
      d=filter(x->!(x in [0,1,p]),smith(simpleroots(R)))
      if isempty(d) return res end
      res*prod(d)do i
                   if i==2 && (2 in b && p!=2) return 2 end
                   if mod(p,i)==1 return i end
                   Mvp(Symbol("q_",i))
                 end
    end
    mu=moebius(P)
    n=stabilizer(W,sort(inclusiongens(H)),onsets)
    n=sum(mu.*l) // length(centralizer(n, HF.phi))
#   InfoChevie("==>", n, "\n")
    n
  end
end
end
