"""
Eigenspaces and `d`-Harish-Chandra series

Let  `Wœï` be a  reflection coset on  a vector space  `V`; that is `Œ¶‚ààGL(V)`
normalizes  the reflection group  `W`. Let `Lwœï`  be a reflection subcoset;
that  is `L` is a  parabolic subgroup of `W`  (the fixator of a subspace of
`V`)  and  `w‚àà  W`  is  such  that  `wŒ¶`  normalizes `L`. There are several
interesting  cases where the *relative group* ``N_W(Lwœï)/L``, or a subgroup
of it normalizing some further data attached to `L`, is itself a reflection
group.

A first example is the case where `œï=1` and `w=1`, `W` is the Weyl group of
a   finite  reductive   group  ``ùêÜ^F``   and  the   Levi  subgroup  ``ùêã^F``
corresponding  to `L` has a cuspidal unipotent character. Then ``N_W(L)/L``
is  a  Coxeter  group  acting  on  the  space  `X(Zùêã)‚äó‚Ñù`.  A  combinatorial
characterization of such parabolic subgroups of Coxeter groups is that they
are  normalized by the  longest element of  larger parabolic subgroups (see
[5.7.1 Lusztig1976](biblio.htm#Lus76)).

A  second  example  is  when  `L`  is  trivial  and  `wœï` is a *`Œ∂`-regular
element*,  that is  the `Œ∂`-eigenspace  ``V_Œ∂`` of  `wœï` contains  a vector
outside  all the reflecting hyperplanes of `W`. Then ``N_W(Lwœï)/L=C_W(wœï)``
is a reflection group in its action on ``V_Œ∂``.

A similar but more general example is when ``V_Œ∂`` is the `Œ∂`-eigenspace of
some  element of  the reflection  coset `Wœï`,  and is  of maximal dimension
among  such `Œ∂`-eigenspaces. Then the set of  elements of `Wœï` which act by
`Œ∂`  on  ``V_Œ∂``  is  a  certain  subcoset  `Lwœï`,  and ``N_W(Lwœï)/L`` is a
reflection group in its action on ``V_Œ∂`` (see [2.5
Lehrer-Springer1999](biblio.htm#LS99)).

Finally,  a  still  more  general  example,  but which only occurs for Weyl
groups  or  Spetsial  reflection  groups,  is  when `ùêã` is a `Œ∂`-split Levi
subgroup  (which means that  the corresponding subcoset  `Lwœï` is formed of
all  the elements which act by `Œ∂` on  some subspace `V_Œ∂` of `V`), and `Œª`
is  a  `d`-cuspidal  unipotent  character  of  `ùêã`  (which  means  that the
multiplicity  of `Œ∂`  as a  root of  the degree  of `Œª`  is the same as the
multiplicity  of `Œ∂` as a root of the generic order of the semi-simple part
of  `ùêÜ`); then ``N_W(Lwœï,Œª)/L`` is a complex reflection group in its action
on `V_Œ∂`.

Further,  in the above cases the relative group describes the decomposition
of a Lusztig induction.

When  ``ùêÜ^F`` is  a finite  reductive group,  and `Œª`  a cuspidal unipotent
character  of  the  Levi  subgroup  ``ùêã^F``,  then the ``ùêÜ^F``-endomorphism
algebra  of  the  Harish-Chandra  induced  representation ``R_ùêã^ùêÜ(Œª)`` is a
Hecke algebra attached to the group ``N_W(L)/L``, thus the dimension of the
characters  of this group describe the multiplicities in the Harish-Chandra
induced.

Similarly, when `ùêã` is a `Œ∂`-split Levi subgroup, and `Œª` is a `d`-cuspidal
unipotent  character of  `ùêã` then  (conjecturally) the ``ùêÜ^F``-endomorphism
algebra  of the Lusztig induced ``R_ùêã^ùêÜ(Œª)``  is a cyclotomic Hecke algebra
for  to the  group ``N_W(Lwœï,Œª)/L``.  The constituents  of ``R_ùêã^ùêÜ(Œª)`` are
called  a  `Œ∂`-Harish-Chandra  series.  In  the  case of rational groups or
cosets,  corresponding to finite  reductive groups, the  conjugacy class of
`Lwœï`  depends  only  on  the  order  `d`  of  `Œ∂`,  so  one  also talks of
`d`-Harish-Chandra  series. These series correspond to `‚Ñì`-blocks where `l`
is  a prime divisor of `Œ¶_d(q)` which  does not divide any other cyclotomic
factor of the order of ``ùêÜ^F``.

The functions 
`relative_degrees,
regular_eigenvalues,
eigenspace_projector,
position_regular_class,
split_levis,
cuspidal`
in this module and the functions in the module `dSeries`
allow to explore these situations.
"""
module Eigenspaces
export relative_degrees, regular_eigenvalues,
  position_regular_class, eigenspace_projector, relative_root, split_levis

using ..Chevie
"""
`relative_degrees(WF,Œ∂::Root1=1)`

Let  `WF` be a reflection group or a  reflection coset and `Œ∂` be a root of
unity.  Then if ``V_Œ∂`` is the `Œ∂`-eigenspace  of some element of `WF`, and
is of maximal dimension among such `Œ∂`-eigenspaces (and if `WF ` is a coset
`W`  is the group of `WF`) then ``N_W(V_Œ∂)/C_W(V_Œ∂)`` is a reflection group
in  its  action  on  ``V_Œ∂``.  The  function `relative_degrees` returns the
reflection  degrees of this complex reflection group, which are a subset of
those of `W`. These degrees are computed by an invariant-theoretic formula:
if   `(d‚ÇÅ,Œµ‚ÇÅ),‚Ä¶,(d‚Çô,Œµ‚Çô)`  are   the  generalized   degrees  of   `WF`  (see
[`degrees`](@ref)) they are the `d·µ¢` such that `Œ∂^{d·µ¢}=Œµ·µ¢`.

The  eigenvalue `Œ∂` can also  be specified by giving  an integer `d` (which
then  specifies  `Œ∂=E(d)`)  or  a  fraction  `a//b`  which  then  specifies
`Œ∂=E(b,a)`. If omitted, `Œ∂` is taken to be `1`.
```julia-repl
julia> W=coxgroup(:E,8)
E‚Çà

julia> relative_degrees(W,4) # the degrees of G‚ÇÉ‚ÇÇ
4-element Vector{Int64}:
  8
 12
 20
 24
```
"""
relative_degrees(W,d::Integer)=relative_degrees(W,E(d))
relative_degrees(W,d::Rational)=relative_degrees(W,Root1(;r=d))
relative_degrees(W,d::Root1)=filter(x->isone(d^x),degrees(W))
relative_degrees(W)=relative_degrees(W,E(1))
relative_degrees(W::Spets,z::Root1)=[d for (d,f) in degrees(W) if Cyc(z)^d==f]

"""
`regular_eigenvalues(W)`

Let `W` be a reflection group or a reflection coset. A root of unity `Œ∂` is
a *regular eigenvalue* for `W` if some element of `W` has a `Œ∂`-eigenvector
which   lies   outside   of   the   reflecting  hyperplanes.  The  function
returns the list of regular eigenvalues for `W`.
```julia-repl
julia> regular_eigenvalues(coxgroup(:G,2))
6-element Vector{Root1}:
   1
  -1
  Œ∂‚ÇÉ
 Œ∂‚ÇÉ¬≤
  Œ∂‚ÇÜ
 Œ∂‚ÇÜ‚Åµ

julia> W=complex_reflection_group(6)
G‚ÇÜ

julia> L=twistings(W,[2])[4]
G‚ÇÜ‚Çç‚ÇÇ‚Çé=G‚ÇÉ‚Äö‚ÇÅ‚Äö‚ÇÅ[Œ∂‚ÇÑ]Œ¶‚Ä≤‚ÇÑ

julia> regular_eigenvalues(L)
3-element Vector{Root1}:
    Œ∂‚ÇÑ
  Œ∂‚ÇÅ‚ÇÇ‚Å∑
 Œ∂‚ÇÅ‚ÇÇ¬π¬π
```
"""
function regular_eigenvalues(W)
  d=degrees(W)
  c=codegrees(W)
  l=filter(x->count(iszero.(d.%x))==count(iszero.(c.%x)),
                  sort(union(divisors.(d)...)))
  sort(vcat(map(x->E.(x,prime_residues(x)),l)...),by=x->(order(x),exponent(x)))
end

function regular_eigenvalues(W::Spets)
  d=degrees(W)
  c=codegrees(W)
  l=union(map(p->divisors(order(Root1(p[2]))*p[1]),d)...)
  res=Root1[]
  for n in l
    p=prime_residues(n)
    p1=filter(i->count(p->E(n,i*p[1])==p[2],d)==count(p->E(n,i*p[1])==p[2],c),p)
    append!(res,E.(n,p1))
  end
  res
end

"""
`position_regular_class(WF,Œ∂::Root1=1)`

Let  `WF` be a reflection group or a  reflection coset and `Œ∂` be a root of
unity  such that some element of `WF` has a non-trivial `Œ∂`-eigenspace. The
function   returns  the   index  of   a  conjugacy   class  of  `WF`  whose
`Œ∂`-eigenspace  is  maximal  (amongst  all  `Œ∂ `-eigenspaces of elements of
`W`).  If no element of `WF`  has a non-trivial `Œ∂`-eigenspace the function
returns `nothing`.

The  eigenvalue `Œ∂` can also  be specified by giving  an integer `d` (which
then  specifies  `Œ∂=E(d)`)  or  a  fraction  `a//b`  which  then  specifies
`Œ∂=E(b,a)`. If omitted, `Œ∂` is taken to be `1`.
```julia-repl
julia> W=coxgroup(:E,8)
E‚Çà

julia> position_regular_class(W,30)
65

julia> W=complex_reflection_group(6)
G‚ÇÜ

julia> L=twistings(W,[2])[4]
G‚ÇÜ‚Çç‚ÇÇ‚Çé=G‚ÇÉ‚Äö‚ÇÅ‚Äö‚ÇÅ[Œ∂‚ÇÑ]Œ¶‚Ä≤‚ÇÑ

julia> position_regular_class(L,7//12)
2
```
"""
position_regular_class(W,d::Integer)=position_regular_class(W,E(d,1))
position_regular_class(W,d::Rational)=position_regular_class(W,Root1(;r=d))
function position_regular_class(W,d::Root1=E(1))
  drank=length(relative_degrees(W,d))
  if drank==0 return nothing end
  return findfirst(x->count(==(d),x)==drank,refleigen(W))
end

"""
`eigenspace_projector(WF,w,Œ∂::Root1=1)`

Let `WF` be a reflection group or a reflection coset, let `w` be an element
of  `WF` and let  `Œ∂` be a  root of unity.  The function returns the unique
`w`-invariant projector on the `Œ∂`-eigenspace of `w`.

The  eigenvalue `Œ∂` can also  be specified by giving  an integer `d` (which
then  specifies  `Œ∂=E(d)`)  or  a  fraction  `a//b`  which  then  specifies
`Œ∂=E(b,a)`. If omitted, `Œ∂` is taken to be `1`.
```julia-repl
julia> W=coxgroup(:A,3)
A‚ÇÉ

julia> w=W(1:3...)
(1,12,3,2)(4,11,10,5)(6,9,8,7)

julia> p=eigenspace_projector(W,w,1//4)
3√ó3 Matrix{Cyc{Rational{Int64}}}:
  (1+Œ∂‚ÇÑ)/4   Œ∂‚ÇÑ/2  (-1+Œ∂‚ÇÑ)/4
  (1-Œ∂‚ÇÑ)/4   1//2   (1+Œ∂‚ÇÑ)/4
 (-1-Œ∂‚ÇÑ)/4  -Œ∂‚ÇÑ/2   (1-Œ∂‚ÇÑ)/4

julia> GenLinearAlgebra.rank(p)
1
```
"""
eigenspace_projector(W,w,d::Integer=1)=eigenspace_projector(W,w,E(d))
eigenspace_projector(W,w,d::Rational)=eigenspace_projector(W,w,Root1(;r=d))
function eigenspace_projector(W,w,d::Root1)
  c=filter(!=(d),refleigen(W)[position_class(W,w)])
  f=reflrep(W,w)
  if isempty(c) one(f)
  else prod(x->f-I*x,c)//prod(x->d-x,c)
  end
end

"""
`split_levis(WF,Œ∂::Root1=1[,ad])`

Let  `WF`  be  a  reflection  group  or  a  reflection  coset.  If `W` is a
reflection group it is treated as the trivial coset 'Spets(W)'.
A  *Levi*  is  a  subcoset  of  the  form `W‚ÇÅF‚ÇÅ` where `W‚ÇÅ` is a *parabolic
subgroup* of `W`, that is the centralizer of some subspace of `V`, and `F‚ÇÅ‚àà
WF` normalizes `W‚ÇÅ`.

Let `Œ∂` be a root of unity. `split_levis` returns a list of representatives
of  conjugacy classes  of `Œ∂`-split  Levis of  `W`. A  `Œ∂`-split Levi  is a
subcoset  of `WF` formed  of all the  elements which act  by `Œ∂` on a given
subspace  `V_Œ∂`. If the additional argument  `ad` is given, it returns only
those subcosets such that the common `Œ∂`-eigenspace of their elements is of
dimension  `ad`. These notions make sense  and thus are implemented for any
complex reflection group.

In  terms of algebraic groups, an `F`-stable Levi subgroup of the reductive
group  `ùêÜ  `  is  `Œ∂`-split  if  and  only  if it is the centralizer of the
`Œ¶`-part  of its center, where `Œ¶` is a cyclotomic polynomial with `Œ∂` as a
root. When `Œ∂=1`, we get the notion of a *split* Levi, which is the same as
a Levi sugroup of an `F`-stable parabolic subgroup of `ùêÜ `.

The  eigenvalue `Œ∂` can also  be specified by giving  an integer `d` (which
then  specifies  `Œ∂=E(d)`)  or  a  fraction  `a//b`  which  then  specifies
`Œ∂=E(b,a)`. If omitted, `Œ∂` is taken to be `1`.
```julia-repl
julia> W=coxgroup(:A,3)
A‚ÇÉ

julia> split_levis(W,4)
2-element Vector{Spets{FiniteCoxeterSubGroup{Perm{Int16},Int64}}}:
 A‚ÇÉ
 A‚ÇÉ‚Çç‚Çé=Œ¶‚ÇÇŒ¶‚ÇÑ

julia> W=spets(coxgroup(:D,4),Perm(1,2,4))
¬≥D‚ÇÑ

julia> split_levis(W,3)
3-element Vector{Spets{FiniteCoxeterSubGroup{Perm{Int16},Int64}}}:
 ¬≥D‚ÇÑ
 ¬≥D‚ÇÑ‚Çç‚ÇÅ‚ÇÉ‚Çé=A‚ÇÇŒ¶‚ÇÉ
 ¬≥D‚ÇÑ‚Çç‚Çé=Œ¶‚ÇÉ¬≤

julia> W=coxgroup(:E,8)
E‚Çà

julia> split_levis(W,4,2)
3-element Vector{Spets{FiniteCoxeterSubGroup{Perm{Int16},Int64}}}:
 E‚Çà‚Çç‚ÇÉ‚ÇÑ‚ÇÇ‚ÇÖ‚Çé=D‚ÇÑ‚Çç‚ÇÅ‚ÇÉ‚ÇÇ‚ÇÑ‚ÇéŒ¶‚ÇÑ¬≤
 E‚Çà‚Çç‚ÇÖ‚Çá‚ÇÇ‚ÇÉ‚Çé=(A‚ÇÅA‚ÇÅ)√ó(A‚ÇÅA‚ÇÅ)Œ¶‚ÇÑ¬≤
 E‚Çà‚Çç‚ÇÉ‚ÇÖ‚ÇÜ‚ÇÅ‚Çé=¬≤(A‚ÇÇA‚ÇÇ)‚Çç‚ÇÅ‚ÇÑ‚ÇÇ‚ÇÉ‚ÇéŒ¶‚ÇÑ¬≤

julia> split_levis(complex_reflection_group(5))
4-element Vector{Spets{PRSG{Cyc{Rational{Int64}}, Int16}}}:
 G‚ÇÖ
 G‚ÇÖ‚Çç‚ÇÅ‚Çé=G‚ÇÉ‚Äö‚ÇÅ‚Äö‚ÇÅŒ¶‚ÇÅ
 G‚ÇÖ‚Çç‚ÇÇ‚Çé=G‚ÇÉ‚Äö‚ÇÅ‚Äö‚ÇÅŒ¶‚ÇÅ
 G‚ÇÖ‚Çç‚Çé=Œ¶‚ÇÅ¬≤
```
"""
split_levis(W,d=E(1))=[L for ad in 0:length(relative_degrees(W,d))
                             for L in split_levis(W,d,ad)]
split_levis(W,d::Integer,ad)=split_levis(W,E(d),ad)
split_levis(W,d::Rational,ad)=split_levis(W,Root1(;r=d),ad)
function split_levis(WF,d::Root1,ad)
  if WF isa Spets W=WF.W
  else W=WF; WF=spets(W)
  end
# if iszero(ad) return [WF] end
  if iszero(ad) return [subspets(WF,1:semisimplerank(WF),Perm())] end
  refs=unique_refls(W)
  eig=refleigen(WF)
  cl=filter(j->count(==(d),eig[j])==ad,1:length(eig))
  res=typeof(subspets(WF,Int[]))[]
  while length(cl)>0
    w=classreps(WF)[cl[1]]
    if rank(W)==0 V=fill(0,0,0)
    else V=lnullspace(reflrep(WF,w)-d*I)
    end
    J=filter(r->iszero(V*coroots(W,r)),refs)
#   println("J=$J\nphi=",w/WF.phi)
    HF=subspets(WF, J, w/WF.phi)
    if isnothing(HF)
      error("subspets($WF,",J,",class#",cl[1],") failed")
    else
      f=intersect(fusion_conjugacy_classes(HF, WF), cl)
      if isempty(f)
        error("fusion is wrong")
        return nothing
      end
#   println("class=",cl[1]," cl=",cl," f=",f)
      cl=setdiff(cl,f)
      H=HF.W
      P=standard_parabolic(W, H)
      if isnothing(P) P=Perm() end
      J=inclusiongens(H).^P
      if P!=Perm() || J!=inclusiongens(H)
        HF=subspets(WF,restriction(W,J),HF.phi^P/WF.phi)
      end
      push!(res, HF)
    end
  end
  return res
end

"""
`standard_parabolic(W::PermRootGroup, H)`

Let  `H` be a  reflection subgroup of  `W`. Returns an  element `w‚àà W` such
that  `H^w` is a standard  parabolic subgroup of `W`  (or nothing if `H` is
not parabolic or not conjugate to a standard parabolic).
"""
function Weyl.standard_parabolic(W::PermRootGroup, H)
  hr=inclusiongens(H,W)
# println("hr=",hr)
  if issubset(hr,eachindex(gens(W))) return Perm() end
  I=combinations(eachindex(gens(W)),length(hr))
  # I=map(x->inclusion(W,x),parabolic_reps(W))
  I=filter(l->length(reflection_subgroup(W,l))==length(H),I)
  try_=function(a)
    H1=H
    w=Perm()
    C=W
    for i in 1:length(a)
      if i>1 C=centralizer(C, gens(W)[a[i-1]]) end
      t=transversal(C,inclusion(H1,i))
      w1=get(t,inclusion(W,a[i]))do
        t=transversal(C,H1(i))
        get(t,W(a[i]),nothing)
      end
      if isnothing(w1) return nothing end
      w=w*w1
      H1=H1^w1
    end
    return w
  end
  for l in I
    for a in arrangements(l, length(l))
      w=try_(a)
      if !isnothing(w) return w end
    end
  end
  InfoChevie("# ****** ",H," is not conjugate to a standard parabolic\n")
  return nothing
end

"""
`relative_root(W,L,i)`

return 'action' of refls(W,i)‚àâ L on X(Z_L)
a namedtuple with root, coroot, eigenvalue, index=i and toparent in W
"""
function relative_root(W,L,i)
# xprintln("W=",W," i=",i," L=",L)
  N=normalizer(reflection_subgroup(W,vcat(inclusiongens(L,W),[i])),L)
  if length(N)==length(L) error("N(L)==L") end
  F=N/L;F.length=div(length(N),length(L))
# xprintln(abelian_gens(elements(F)))
  d=length(F)
  if  !iscyclic(F)  error("in theory N/L expected to be cyclic") end
# println("d=$d ",order.(elements(F)))
  for rc in filter(x->order(x)==d,elements(F))
    m=central_action(L,reflrep(L,rc.phi))
    r=asreflection(m)
    if r===nothing error("not a reflection --- This should not happen") end
#   println("rc=$rc")
    if r.eig==E(d)
      rc=filter(c->central_action(L,reflrep(L,c))==m,classreps(N))
#     println("rc=$rc")
      c=unique!(sort!(position_class.(Ref(W),rc)))
      m=maximum(count.(isone,refleigen(W)[c]))
      m=filter(x->count(isone,refleigen(W)[x])==m,c)
      m=filter(x->position_class(W,x) in m,rc)
      if any(x->order(x)==d,m)  m=filter(x->order(x)==d,m) end
      return (root=r.root,coroot=r.coroot,eigenvalue=r.eig,index=i,
              toparent=m[1])
    end
  end
  error("no root found for reflection along root ",i,"\n")
end

function Weyl.relative_group(W,J::Vector{<:Integer},indices=false)
# println("relative_group: W=$W J=$J indices=$indices")
  res = Dict{Symbol, Any}(:callarg => joindigits(J))
  if indices!=false res[:callarg]*=","*joindigits(indices) end
# res = CHEVIE[:GetCached](W, "RelativeGroups", res, x->x[:callarg])
# if length(keys(res))>1 return res end
  L=reflection_subgroup(W, J)
  if length(J)==0
    W.fromparent=w->PermX(W,central_action(L, reflrep(L,w)))
    W.relative_indices=eachindex(gens(W))
    return W
  end
  if indices==false
    indices=filter(x->!(refls(W, x) in L),eachindex(gens(W)))
  end
  if sort(inclusion(L))==sort(inclusion(W))
    res[:relative_indices]=Int[]
    res[:fromparent]=w->Perm()
    res[:rank]=0
    return PRG(Perm{Int16}[],one(W),Matrix{Int}[],Vector{Int}[],Vector{Int}[],res)
  end
  res[:roots]=empty(roots(W))
  res[:simpleCoroots]=empty(roots(W))
  res[:relative_indices]=Int[]
  res[:toparent]=empty(gens(W))
  for R in filter(!isnothing,map(r->relative_root(W,L,r),indices))
    p=findfirst(==(R.root),res[:roots])
    if p===nothing || p!=findfirst(==(R.coroot),res[:simpleCoroots])
      push!(res[:roots], R.root)
      push!(res[:simpleCoroots], R.coroot)
      push!(res[:relative_indices], R.index)
      push!(res[:toparent], R.toparent)
    end
  end
  N=length(normalizer(W,L))//length(L)
  res[:roots]=improve_type(res[:roots])
  res[:simpleCoroots]=improve_type(res[:simpleCoroots])
# println(res[:roots],res[:simpleCoroots])
  R=PRG(res[:roots], res[:simpleCoroots])
  if N==length(R)
    merge!(R.prop,res)
    R.fromparent=w->PermX(R, central_action(L, reflrep(L,w)))
#   InfoChevie("W_",W,"(L_",res[:callarg],")==",R,"<",join(R.prop[:relative_indices]),">")
    # printTeX(rio(),"W_",W,"(L_{",res[:callarg],"})==",R,
    #             "<",joindigits(R.relative_indices),">\n")
    return R
  end
  printTeX(rio(),"# warning: W_",W,"(L_{",res[:callarg],"})",":size ",N,
   " not generated by ",joindigits(indices,"!=")," ==>size ",length(R),"\n")
  print("#           trying all other roots\n")
  indices=filter(r->!(refls(W,r) in gens(W)) && !(refls(W,r) in L), 
                  eachindex(roots(W)))
  for r in indices
    l=relative_root(W, L, r)
    if l.root in res[:roots] continue end
    nr=vcat(res[:roots],[l.root])
    ncr=vcat(res[:simpleCoroots],[l.coroot])
    R=PRG(nr,ncr)
    if N==length(R)
      res[:roots]=nr
      res[:simpleCoroots]=ncr
      push!(res[:relative_indices], l.index)
      push!(res[:toparent], l.toparent)
      empty!(R.prop);merge!(R.prop,res)
      R.fromparent=w->PermX(R, central_action(L, reflrep(L,w)))
      return R
    end
  end
  error("relgroup not found")
end

end
